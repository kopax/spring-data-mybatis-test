def springBootVersionDef = '1.5.7.RELEASE'

buildscript {

    ext {
        gradleReleaseVersion = '2.4.1'
        springBootVersion = '1.5.7.RELEASE'
        springSessionJdbcVersion = '1.3.1.RELEASE'
        springSecurityOauth2Version = '2.2.0.RELEASE'
        springSecurityJwtVersion = '1.0.8.RELEASE'
        propdepsPluginVersion = '0.0.7'
        gsonVersion = '2.8.0'
        apachUtilsVersion = '3.5'
        apachedsServerJndiVersion = '1.5.5'
        postgresqlVersion = '42.0.0.jre7'
        springBootStarterDataMybatisVersion = '1.0.17.RELEASE'
        mybatisTypehandlersJsr310Version  = '1.0.1'
        jacksonDatatypeJsr310Version = '2.9.0.pr1'
        buildInfoExtractorGradleVersion = '4.4.15'
        lombokVersion = '1.16.18'
        flywayVersion = '4.2.0'
    }

    repositories {
        mavenCentral()
        maven {
            url "http://repo.spring.io/plugins-release"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework.build.gradle:propdeps-plugin:${propdepsPluginVersion}")
        classpath("net.researchgate:gradle-release:${gradleReleaseVersion}")
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:${buildInfoExtractorGradleVersion}")
    }
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'application'

mainClassName = 'com.domain.api.Application'

task createPom << {
    pom {
        project {
            groupId 'com.domain.api'
            artifactId 'gs-gradle'
            version '0.1.0'
            properties {
                project {
                    build {
                        sourceEncoding 'UTF-8'
                    }
                }
                maven {
                    compiler {
                        source '1.8'
                        target '1.8'
                    }
                }
            }

            inceptionYear '2008'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.withXml {
        asNode().appendNode('build').appendNode('plugins').with {
            appendNode('plugin').with {
                appendNode('groupId', 'org.springframework.boot')
                appendNode('artifactId', 'spring-boot-maven-plugin')
                appendNode('version', "${springBootVersionDef}")
                appendNode('executions').appendNode('execution').appendNode('goals').with {
                    appendNode('goal', 'repackage')
                }
            }
            appendNode('plugin').with {
                appendNode('groupId', 'org.apache.maven.plugins')
                appendNode('artifactId', 'maven-jar-plugin')
                appendNode('version', "3.0.2")
                appendNode('configuration').appendNode('archive').appendNode('manifest').with {
                    appendNode('addClasspath', "true")
                    appendNode('classpathPrefix', "lib/")
                    appendNode('mainClass', "com.domain.api.Application")
                }
            }
        }
    }.writeTo("pom.xml")
}

// tag::repositories[]
repositories {
    mavenCentral()
}
// end::repositories[]

// tag::jar[]
jar {
    baseName = 'gs-gradle'
    version =  '0.1.0'
}
// end::jar[]

// tag::dependencies[]
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-data-rest:${springBootVersion}")
    compile("org.postgresql:postgresql:${postgresqlVersion}")
    compile("org.flywaydb:flyway-core:${flywayVersion}")

    compile("com.ifrabbit:spring-boot-starter-data-mybatis:${springBootStarterDataMybatisVersion}")
    compile("org.mybatis:mybatis-typehandlers-jsr310:${mybatisTypehandlersJsr310Version}")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeJsr310Version}")

//    compile('org.apache.directory.studio:org.apache.commons.collections:3.2.1')
//    compile 'org.kie.modules:org-apache-commons-collections-main:6.5.0.Final'

    compile("org.projectlombok:lombok:${lombokVersion}")
    testCompile "junit:junit:4.12"
}
// end::dependencies[]

// tag::wrapper[]
// end::wrapper[]
